apply from: '../jacoco.gradle'

buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.24.5'

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
    maven { url "http://dl.bintray.com/novoda/maven" }
    google()
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: 'io.fabric'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.2'

    defaultConfig {
        applicationId "solutions.alterego.android.unisannio"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 2
        versionName "20151214"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            storeFile file("../../android-keystore")
            storePassword ""
            keyAlias "android"
            keyPassword ""
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
            buildConfigField "String", "AMPLITUDE_KEY", "\"$System.env.AMPLITUDE_KEY\""
        }

        release {
            buildConfigField "String", "AMPLITUDE_KEY", "\"$System.env.AMPLITUDE_KEY\""

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    testOptions {
        unitTests.returnDefaultValues = true

        unitTests.all {
            jvmArgs '-XX:MaxPermSize=256m'
        }
    }
}

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor 10 * 60, 'seconds'
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation project(':core')

    implementation "com.android.support:multidex:1.0.3"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.5'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.22.5"

    implementation "com.jakewharton.timber:timber:$timberVersion"

    implementation "com.android.support:support-v4:$support_libs"
    implementation "com.android.support:appcompat-v7:$support_libs"
    implementation "com.android.support:cardview-v7:$support_libs"
    implementation "com.android.support:recyclerview-v7:$support_libs"
    implementation "com.android.support:design:$support_libs"
    implementation "com.google.android.gms:play-services-maps:$play_services"
    implementation "com.google.android.gms:play-services-analytics:$play_services"

    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    compileOnly 'javax.annotation:jsr250-api:1.0'

    implementation 'com.android.support:design:27.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:support-vector-drawable:27.1.0'

    implementation "org.jsoup:jsoup:$jsoupVersion"

    implementation 'io.reactivex:rxjava:1.3.6'
    implementation 'io.reactivex:rxandroid:1.2.1'

    implementation 'com.amplitude:android-sdk:2.16.0'

    // DEBUGGING
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'

    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') { transitive = true }
    // -- DEBUGGING

    // UI
    implementation 'com.joanzapata.iconify:android-iconify:2.2.2'
    implementation 'de.hdodenhof:circleimageview:2.1.0'
    implementation 'com.novoda:simple-chrome-custom-tabs:0.1.5'

    // Utils
    implementation 'net.danlew:android.joda:2.9.9.1'

    // TESTING
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation 'com.willowtreeapps.assertk:assertk:0.9'

    testImplementation('junit:junit:4.12') {
        exclude module: 'hamcrest'
        exclude module: 'hamcrest-core'
    }
    testImplementation 'org.easytesting:fest-assert-core:2.0M10'
    testImplementation('org.mockito:mockito-core:1.10.19') {
        exclude module: 'hamcrest'
        exclude module: 'hamcrest-core'
    }
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation "org.jsoup:jsoup:$jsoupVersion"

    androidTestImplementation 'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'
}
